Using System
Using System.Data
Using System.Configuration
Using System.Collections.Specialized
Using System.Collections
Using System.Web
Using System.Web.Security
Using System.Web.UI
Using System.Web.UI.WebControls
Using System.Web.UI.WebControls.WebParts
Using System.Web.UI.HtmlControls

BegClass CustomerForm Partial(*Yes) Access(*Public) Extends(System.Web.UI.Page)

    DclDB DGDB 
    DclFld dt Type(DataTable) 

    DclFld crud Type(CustomerCrud) 
    DclFld crudhelp Type(CrudHelpers) 
    DclFld StateHelper Type(AppStateHelper) New(*This.Context) 

    BegSr Page_Load Access(*Private) Event(*This.Load)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)
            
        DclFld CustomerNumber Type(*Integer4) 
        DclFld statesList Type(ListItemCollection) 

        DGDB.DBName = StateHelper.GetApplicationValueAsString('dbname') 
        DGDB.User = StateHelper.GetSessionValueAsString('user') 
        DGDB.Password = StateHelper.GetSessionValueAsString('password') 

        Connect DGDB 
        crud = *New CustomerCrud(DGDB)
        crudhelp = *New CrudHelpers(*This) 

        If (NOT Page.IsPostBack)
            StatesList = (*New StateList(DGDB)).GetList()
            crudhelp.LoadListControl(CMState, StatesList)

            CustomerNumber = StateHelper.GetSessionValueAsInt32('customer-number') 
            labelCustomerNumber.Text = CustomerNumber

            dt = Crud.ReadCustomer(CustomerNumber) 
            crudhelp.PopulateUI(dt) 
        Else
        EndIf 
    EndSr 

    BegSr Page_Unload Access(*Private) Event(*This.Unload)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        Disconnect DGDB 
    EndSr

	BegSr buttonUpdateCustomer_Click Access(*Private) Event(*This.buttonUpdateCustomer.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        DclFld CustomerNumber Type(*Integer4) 

        CustomerNumber = Convert.ToInt32(CMCustNo.Text) 
        
        dt = Crud.ReadCustomer(CustomerNumber) 
        crudhelp.PopulateFormat(dt) 

        Session['first-customer-name'] = CMName.Text
        Session['first-customer-number'] = CMCustNo.Text

        Crud.UpdateCustomer(CustomerNumber, dt) 
        Response.Redirect("CustomerList.aspx") 
	EndSr

EndClass
