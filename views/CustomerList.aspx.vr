Using System
Using System.Data
Using System.Web.UI.WebControls

BegClass views_CustomerList Partial(*Yes) Access(*Public) Extends(System.Web.UI.Page)

    DclDB DGDB 
    DclFld CustNameList Type(CustomerByNameList) 
    DclConst ROWS_TO_DISPLAY Value(12) 

    DclFld StateHelper Type(AppStateHelper) New(*This.Context) 

    BegSr Page_Load Access(*Private) Event(*This.Load)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        DclFld dt Type(DataTable) 
        DclFld CustomerName Type(*String) 
        DclFld CustomerNumber Type(*Integer4) 

        DGDB.DBName = StateHelper.GetApplicationValue('dbname').ToString()
        //DGDB.User = StateHelper.GetSessionValueAsString('user')
        //DGDB.Password = StateHelper.GetSessionValueAsString('password')

        Connect DGDB 
        CustNameList = *New CustomerByNameList(DGDB, ROWS_TO_DISPLAY) 

        If (NOT Page.IsPostBack)
            If  ArePositionToValuesProvided()
                CustomerName = StateHelper.GetSessionValueAsString('first-customer-name')
                CustomerNumber = StateHelper.GetSessionValueAsInt32('first-customer-number')

                dt = PositionListTo(CustomerName, CustomerNumber) 
                BindGridView(dt) 
            Else 
                dt = CustNamelist.ReadPage()            
                BindGridView(dt) 
            EndIf 
        Else
        EndIf
    EndSr

    BegFunc ArePositionToValuesProvided Type(*Boolean) 
        LeaveSr Session['first-customer-name'] <> *Nothing AND Session['first-customer-number'] <> *Nothing
    EndFunc

    BegSr BindGridView 
        DclSrParm dt Type(DataTable) 

        gridviewCust.DataSource = dt
        gridviewCust.DataBind()

        Session['lastCMName'] = CustNameList.LastCMName.Trim()
        Session['lastCMCustNo'] = CustNameList.LastCMCustNo

        Session['first-customer-name'] = CustNameList.FirstCMName.Trim()
        Session['first-customer-number'] = CustNameList.FirstCMCustNo

        buttonNext.Enabled = CustNameList.MoreRows
        If buttonNext.Enabled 
            labelTableFooter.Text = 'More records to show'
        Else
            labelTableFooter.Text = 'No more records to show'
        EndIf 
    EndSr 

    BegSr Page_Unload Access(*Private) Event(*This.Unload)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        Disconnect DGDB 
        //
        // Called when the page is unloaded.
        //
    EndSr

	BegSr gridviewCust_SelectedIndexChanged Access(*Private) Event(*This.gridviewCust.SelectedIndexChanged)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)
		
        DclFld SelectedRow Type(*Integer4) 

        SelectedRow = gridviewCust.SelectedIndex

        DclFld CustomerNumber Type(*Integer4) 
        DclFld CustomerName Type(*String) 

        CustomerNumber = Convert.ToInt32(gridviewCust.DataKeys[SelectedRow]['customer_cmcustno'])
        CustomerName = gridviewCust.DataKeys[SelectedRow]['customer_cmname'].ToString()
	EndSr

	BegSr buttonNext_Click Access(*Private) Event(*This.buttonNext.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        DclFld dt Type(DataTable) 

        CustNameList.LastCMName = StateHelper.GetSessionValueAsString('lastCMName')
        CustNameList.LastCMCustNo = StateHelper.GetSessionValueAsInt32('lastCMCustNo')
        
        dt = CustNamelist.ReadNextPage()

        BindGridView(dt) 		
	EndSr

	BegSr buttonPositionTo_Click Access(*Private) Event(*This.buttonPositionTo.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        DclFld dt Type(DataTable) 

        If textboxPositionTo.Text.Trim() <> String.Empty 
            dt = PositionListTo(textboxPositionTo.Text)
            If dt.Rows.Count <> 0 
                BindGridView(dt) 		
                textboxPositionTo.Text = String.Empty 
            EndIf 
        EndIf 
	EndSr

	BegSr gridviewCust_RowCommand Access(*Private) Event(*This.gridviewCust.RowCommand)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.Web.UI.WebControls.GridViewCommandEventArgs)

        DclFld CommandName    Type(*String)
        DclFld SelectedIndex  Type(*Integer4) 
        DclFld SelectedRow    Type(GridViewRow)
        DclFld CustomerNumber Type(*Integer4)

        CommandName = e.CommandName
        SelectedIndex = e.CommandArgument.ToString() 
        SelectedRow = gridviewCust.Rows[SelectedIndex]
        CustomerNumber = gridviewCust.DataKeys[SelectedIndex]["customer_cmcustno"].ToString()		

        If CommandName = 'ActionEdit'
            Session['customer-number'] = CustomerNumber
            Response.Redirect('customerform.aspx')
        EndIf 
	EndSr

    BegFunc PositionListTo Type(DataTable) 
        DclSrParm CustomerName Type(*String) 

        LeaveSr PositionListTo(CustomerName, 0) 
    EndFunc             

    BegFunc  PositionListTo Type(DataTable) 
        DclSrParm CustomerName Type(*String) 
        DclSrParm CustomerNumber Type(*Integer4) 

        DclFld dt Type(DataTable) 

        dt = CustNamelist.PositionPageTo(CustomerName.Trim(), CustomerNumber)
        If dt.Rows.Count = 0 
            LeaveSr *Nothing 
        EndIf 

        LeaveSr dt
    EndFunc 


EndClass
